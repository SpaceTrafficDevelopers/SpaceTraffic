<program> :== <command_group> EOF 

<command_group> :== <command> | <command> EOL <command_group>

<command> :== e | <let_command> | DIM <variable>'['<range>']' | <if_command> | <for_command> |
              GO TO <label_name> | <label_name>: | REM <any text until EOL> | PRINT <string_expresion> |
              <spacetraffic_command>

<let_command> :== LET <num_variable> = <expresion> | LET <string_variable> = <string_expresion> |
                  LET <num_variable>[] = <num_array_factor> | LET <string_variable>[] = <string_array_factor>
<if_command> :== IF <expresion> THEN <command> | IF <expresion> THEN EOL <command_group> EOL ENDIF
<for_command> :== FOR <num_variable> = <expresion> TO <expresion> EOL <command_group> EOL NEXT <num_variable> | 
                  FOR <num_variable> = <expresion> TO <expresion> STEP <expresion> EOL <command_group> EOL NEXT <num_variable>

<spacetraffic_command> :== FLY TO <string_expresion_list> | LDCARGO <string_expresion> AMNT <expresion> FROM <string_expresion> |
                  ULDCARGO <string_expresion> AMNT <expresion> TO <string_expresion> | <buy_command> | <sell_command> | REPAIR
                  
<buy_command> :== BUY <string_expresion> AMNT <expresion> MAXP <expresion> |
                  BUY <string_expresion> AMNT <expresion> MAXP <expresion> FROM <string_expresion>   
<sell_command> :== SELL <string_expresion> AMNT <expresion> MINP <expresion> |
                  SELL <string_expresion> AMNT <expresion> MINP <expresion> TO <string_expresion>   

<string_expresion_list> :== <string_expresion> | <string_expresion>, <string_expresion_list>

<string_expresion> :== <string> | <string> + <string_expresion>

<string> :== <string_variable> | '"'<ascii chars other than ">'"' | (<string_expresion>)

<function> :== INT <faktor> | SQRT <faktor> | LENS <string_factor> | LEN <variable>[] | RND | <spacetraffic_function>

<spacetraffic_function> :== <get_price_function> | <get_bases_function> | <get_planets_function> | GET STARSYSTEMS |
                  GET FUEL | GET SPACE <string> | GET FLYTIME <string> | <get_supply_function> | GET EXITS | GET WEAR

<get_price_function> :== GET PRICE <string> | GET PRICE <string> FROM <string>
<get_bases_function> :== GET BASES | GET BASES IN <string>
<get_planets_function> :== GET PLANETS | GET PLANETS IN <string>
<get_supply_function> :== GET SUPPLY <string> | GET SUPPLY <string> FROM <string> | GET SUPPLY <string> IN <string> 

<variable> :== <string_variable> | <num_variable>
<num_variable> :== <int_variable> | <double_variable>

<string_variable> :== <name>$ | <name>$'['<index>']'
<int_variable> :== <name>% | <name>%'['<index>']'
<double_variable> :== <name># | <name>#'[v<index>']'

<expresion> :== <and_expresion> | <expresion> OR <and_expresion>
<and_expresion> :== <logic_expresion> | <and_expresion> AND <logic_expresion>
<logic_expresion> :== <logic_term> | <logic_expresion> = <logic_term> | <logic_expresion> < <logic_term> |
                <logic_expresion> > <logic_term> | <logic_expresion> <= <logic_term> | 
                <logic_expresion> >= <logic_term> | <logic_expresion> <> <logic_term>
<logic_term> :== <term> | <logic_term> + <term> | <logic_term> - <term>
<term> :== <logic_factor> | <term> * <logic_factor> | <term> / <logic_factor> | 
            <term> MOD <logic_factor> | <term> DIV <logic_factor>
<logic_factor> :== <factor> | NOT <factor>
<factor> :== (<expresion>) | <function> | <num_variable> | <number> | - <factor>

<label_name> :== <name>

<name> :== <letter> | <letter><letters_and_digits>
<letters_and_digits> :== <letter_or_digit> | <letter_or_digit><letters_and_digits>
<letter_or_digit> :== <letter> | <digit>
<letter> :== A..Za..z
<digit> :== 0..9

<int_number> :== <digit> | <digit><int_number>
<double_number> :== <int_number>.<int_number> | .<int_number> | <int_number>.

<index> :== <expresion>
<range> :== <expresion>

